PROGRAM                     → TYPE IDENTIFIER DECL PROGRAM | ε
DECL                        → FUNCDECL | VARDECL 
FUNCDECL                    → PARALIST COMPOUNDSTMT 
VARDECL                     → INITDECLARATOR INITDECLARATORLIST ; 
INITDECLARATORLIST          → , IDENTIFIER INITDECLARATOR INITDECLARATORLIST | ε
INITDECLARATOR              → [ DECLARATOR ] SUBINITDECLARATOR | ε
SUBINITDECLARATOR           → = INITIALIZER | ε
DECLARATOR                  → ε | intliteral  
INITIALIZER                 → EXPR | { EXPR SUBINITIALIZER } 
SUBINITIALIZER              → , EXPR SUBINITIALIZER | ε
TYPE                        → void | boolean | int | float 
IDENTIFIER                  → id 
COMPOUNDSTMT                → { SUBCOMPOUNDSTMT } 
SUBCOMPOUNDSTMT             → TYPE IDENTIFIER VARDECL SUBCOMPOUNDSTMT | STMT SUBCOMPOUNDSTMT | ε
STMT                        → COMPOUNDSTMT | IFSTMT | FORSTMT | WHILESTMT | BREAKSTMT | CONTINUESTMT | RETURNSTMT | EXPRSTMT 
IFSTMT                      → if ( EXPR ) STMT ELSESTMT 
ELSESTMT                    → else STMT | ε
FORSTMT                     → for ( MAYBEEXPR ; MAYBEEXPR ; MAYBEEXPR ) STMT 
MAYBEEXPR                   → EXPR | ε
WHILESTMT                   → while ( EXPR ) STMT 
BREAKSTMT                   → break ; 
CONTINUESTMT                → continue ; 
RETURNSTMT                  → return MAYBEEXPR ; 
EXPRSTMT                    → EXPR ;  | semicolon
EXPR                        → CONDOREXPR SUBEXPR 
SUBEXPR                     → = CONDOREXPR SUBEXPR  | ε
CONDOREXPR                  → CONDANDEXPR SUBCONDOREXPR 
SUBCONDOREXPR               →  | | CONDANDEXPR SUBCONDOREXPR | ε
CONDANDEXPR                 → EQUALITYEXPR SUBCONDANDEXPR 
SUBCONDANDEXPR              → && EQUALITYEXPR SUBCONDANDEXPR | ε
EQUALITYEXPR                → RELATIONALEXPR SUBEQUALITYEXPR 
SUBEQUALITYEXPR             → == RELATIONALEXPR SUBEQUALITYEXPR | != RELATIONALEXPR SUBEQUALITYEXPR | ε
RELATIONALEXPR              → ADDITIVEEXPR SUBRELATIONALEXPR 
SUBRELATIONALEXPR           → < ADDITIVEEXPR SUBRELATIONALEXPR | <= ADDITIVEEXPR SUBRELATIONALEXPR | > ADDITIVEEXPR SUBRELATIONALEXPR | >= ADDITIVEEXPR SUBRELATIONALEXPR | ε
ADDITIVEEXPR                → MULTIPLICATIVEEXPR SUBADDITIVEEXPR
SUBADDITIVEEXPR             → + MULTIPLICATIVEEXPR SUBADDITIVEEXPR | - MULTIPLICATIVEEXPR SUBADDITIVEEXPR  | ε
MULTIPLICATIVEEXPR          → UNARYEXPR SUBMULTIPLICATIVEEXPR 
SUBMULTIPLICATIVEEXPR       → * UNARYEXPR SUBMULTIPLICATIVEEXPR | / UNARYEXPR SUBMULTIPLICATIVEEXPR  | ε
UNARYEXPR                   → + UNARYEXPR | - UNARYEXPR | ! UNARYEXPR | PRIMARYEXPR 
PRIMARYEXPR                 → IDENTIFIER SUBPRIMARYEXPR | ( EXPR ) | intliteral | floatliteral | boolliteral | stringliteral 
SUBPRIMARYEXPR              → ε | ARGLIST | [ EXPR ] 
PARALIST                    → ( PROPERPARALIST )                             
PROPERPARALIST              → PARADECL SUBPROPERPARALIST | ε
SUBPROPERPARALIST           → , PARADECL SUBPROPERPARALIST | ε
PARADECL                    → TYPE IDENTIFIER SUBPARADECL
SUBPARADECL                 → [ DECLARATOR ] | ε
ARGLIST                     → ( PROPERARGLIST ) 
PROPERARGLIST               → EXPR SUBPROPERARGLIST | ε
SUBPROPERARGLIST            → , EXPR SUBPROPERARGLIST | ε